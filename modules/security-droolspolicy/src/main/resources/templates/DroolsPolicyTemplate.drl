template header
POLICY_COUNTER
PRIORITY
CHECK_PRIORITY
URI_PATTERN
QUERY_PARAMS_CONDITIONS
REQUEST_TYPE
ALLOWED_ROLES
DENIED_ROLES
ALLOWED_USERS
DENIED_USERS

package io.liveoak.security.policy.drools.impl;

import io.liveoak.security.policy.drools.impl.RulesProcessingResult;
import io.liveoak.security.policy.drools.impl.EndSemaphore;
import io.liveoak.security.policy.drools.impl.URIMatcher;
import io.liveoak.security.policy.drools.impl.URIMatcherCache;
import io.liveoak.security.policy.drools.impl.RequestContextDecorator;
import io.liveoak.security.policy.drools.impl.ResourceParamsDecorator;
import io.liveoak.security.policy.drools.impl.RolesContainer
import io.liveoak.common.security.AuthzDecision;
import io.liveoak.spi.RequestType;
import io.liveoak.spi.ResourcePath;
import io.liveoak.spi.SecurityContext;
import io.liveoak.spi.state.ResourceState;
import org.jboss.logging.Logger;
import java.util.List;
import java.util.Arrays;
import java.util.ArrayList;
import java.util.Map;
import java.util.regex.Pattern;
import java.util.regex.Matcher;

import static io.liveoak.security.policy.drools.impl.LoggerHelper.logger;

dialect "mvel"

template "URI Policy template"
rule "URIPolicyRule_@{POLICY_COUNTER}"
salience @{PRIORITY}
when
  $rulesProcessingResult: RulesProcessingResult( isAlreadyProcessedRule("URIPolicyRule_@{POLICY_COUNTER}") == false );
  $endSemaphore: EndSemaphore( finished == false );
  $securityContext: SecurityContext();
  $requestState: ResourceState();
  $responseState: ResourceState();
  $resourceParams: ResourceParamsDecorator($sort: safeValue('sort'), $expand: safeValue('expand'), $fields: safeValue('fields'),
                                           $offset: intValue('offset'), $limit: intValue('limit'), $q: safeValue('q'));
  URIMatcherCache($uriMatcher: getURIMatcher("URIPolicyRule_@{POLICY_COUNTER}"));
  $request: RequestContextDecorator(
                            uriMatches("URIPolicyRule_@{POLICY_COUNTER}", @{URI_PATTERN}, resourcePath, $uriMatcher) &&
                            requestConditionsMatches("URIPolicyRule_@{POLICY_COUNTER}", (@{QUERY_PARAMS_CONDITIONS}) ) &&
                            requestTypeMatches("URIPolicyRule_@{POLICY_COUNTER}", requestType, "@{REQUEST_TYPE}")
                          );
then
  if (logger().isTraceEnabled()) {
    logger().trace("Triggered rule: URIPolicyRule_@{POLICY_COUNTER} . All conditions matched");
  }

  RolesContainer container = new RolesContainer();
  container.addAllAllowedRoles( Arrays.asList(new String[] { @{ALLOWED_ROLES} }) );
  container.addAllDeniedRoles( Arrays.asList(new String[] { @{DENIED_ROLES} }) );
  container.addAllAllowedUsers( Arrays.asList(new String[] { @{ALLOWED_USERS} }) );
  container.addAllDeniedUsers( Arrays.asList(new String[] { @{DENIED_USERS} }) );

  AuthzDecision decision = container.isRequestAllowed($request);

  modify ($rulesProcessingResult) {
    mergeDecision(decision);
  }
  modify ($rulesProcessingResult) {
    addProcessedRule("URIPolicyRule_@{POLICY_COUNTER}");
  }
  modify ($rulesProcessingResult) {
    lastProcessedPriority = @{PRIORITY}
  }
end
end template

template "Check finished"
rule "CheckFinished_@{POLICY_COUNTER}"
salience @{CHECK_PRIORITY}
when
  RulesProcessingResult(decision == AuthzDecision.ACCEPT || == AuthzDecision.REJECT, lastProcessedPriority > @{CHECK_PRIORITY})
  $endSemaphore: EndSemaphore( finished == false )
then
  if (logger().isTraceEnabled()) {
    logger().trace("Triggered rule: CheckFinished_@{POLICY_COUNTER}, priority: " + @{CHECK_PRIORITY} + " . Going to stop processing other URI rules");
  }
  modify ($endSemaphore) {
    finished = true;
  }
end
end template